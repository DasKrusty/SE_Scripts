*TO DO
*   Legs
*       used for connecting to modules
*       used for picking up things
*   Check all screens for correct info
*   See why SDS searchllights not working / if cant make work around with all lights onlinetostandby
*   See how to fix double tab module issu#End Region
*   See for module if can lift module up = remove torque and brake from hinges and piston
*   Add ascent script and possibly a automated transport script

*Feautres
*   Automated landing procedure
*   Automated connector procedure

@variables{
    * Abreviated names for blocks
    AIVE = "UCX-11 Air Vent Fan"
    ANTE = "UCX-11 Antenna"
    BATT = "UCX-11 Battery"
    COLA = "UCX-11 Connector Landing"
    COMA = "UCX-11 Connector Main"
    COSE = "UCX-11 Connector Second"
    EVC1 = "UCX-11 Event Controller - Land"
    EVC2 = "UCX-11 Event Controller - Cockpit"
    EVC3 = "UCX-11 Event Controller - Altitude"
    GYRO = "UCX-11 Gyroscope"
    HEVE = "UCX-11 Heat Vent"
    HIL1 = "UCX-11 Hinge Landing One"
    HIL2 = "UCX-11 Hinge Landing Two"
    HYTA = "UCX-11 Hydrogen Tank"
    LCIN = "UCX-11 LCD Info"
    LIPA = "UCX-11 Light Panel Floor"
    LIST = "UCX-11 Light Status"
    LISE = "UCX-11 Light Search"
    LINA = "UCX-11 Light Nav"
    LIFL = "UCX-11 Light Flight"
    LIGH = "UCX-11 Light"
    MALO = "UCX-11 Maglock"
    MEBL = "UCX-11 Merge Landing"
    MEBF = "UCX-11 Merge Landing Front"
    O2H2 = "UCX-11 O2/H2 Generator"
    PILA = "UCX-11 Piston Landing"
    TURR = "UCX-11 Turret" 
    THRU = "UCX-11 Hydrogen Thruster"
    TISH = "UCX-11 Timer Shutdown"
    TIST = "UCX-11 Timer Standby"
}

@module_toggle{
    * Safety - if landed dont do anything
    If Status of UCX-11 Connector Landing Front = Unconnected {
        Write to \LCIN = "Not Connected"
        Delay 3000
        @module_lock
    }
    Else If Status of UCX-11 Merge Landing Front = Merged {
        Write to \LCIN = "Is Connected"
        Delay 3000
        @module_release
    }
    
}

@module_lock{
    * Safety - if landed dont do anything
    * Triggered by Menu
    Write to \LCIN = "Module Mode Activated"
    if Status of UCX-11 Maglock BL = Locked {
        Delay 1000
        WriteLine to \LCIN = "ERROR... Check Maglocks"
    }
    else {
        OnOff_Off \EVC1
        OnOff_Off \EVC2
        OnOff_On \MEBL
        OnOff_On \LIPA
        Rotate \HIL1 to 65 at 1
        Delay 3000
        Rotate \HIL2 to -25 at 2
        Move \PILA to 2 at 1
        OnOff_Off \EVC3
        Write to \LCIN = "Arms Ready"
        Delay 3000
        OnOff_On \MEBL
        Delay 3000
        Lock \COLA
        Color of \LIPA = 128:0:128
        Write to \LCIN = "Module Good"
    }
}

@module_release{
    Write to \LCIN = "Module Drop Prepping"
    OnOff_Off \MEBL
    Delay 1000
    Unlock \COLA
    Color of \LIST = 0:255:0
    WriteLine to \LCIN = "Module Released"
    Delay 1000
    Color of \LIST = 255:255:255
    OnOff_On \EVC3
    @low_alt
}

@info {
    Write to \LCIN = EAP - V.2.68
    WriteLine to \LCIN = FSD - V1.00
    WriteLine to \LCIN = SDS - V0.15
    WriteLine to \LCIN = Craft - V0.94
}

@low_alt{
    * Triggered by Event Controller checking Altitude
    Color of \LIPA = 255:255:255
    OnOff_On \LIPA
    Write to \LCIN = "LOW ALTITUDE"
    Rotate \HIL1 to 30 at 3
    Delay 500
    Rotate \HIL2 to 30 at 6
    Move \PILA to 0.5 at 1
    OnOff_On \EVC1
}

@high_alt{
    * Triggered by Event Controller checking Altitude
    OnOff_Off \LIPA
    Write to \LCIN = "HIGH ALTITUDE"
    Rotate \HIL1 to 1 at 1
    Rotate \HIL2 to -1 at 1
    Move \PILA to 0 at 1
    OnOff_Off \EVC1
    OnOff_Off \EVC2
}

@land{
    * Triggered by Event Controller checking if Maglock ready to lock
    Color of \LIPA = 255:255:0
    Write to \LCIN = "Ready to Land"
    Damp of MyShip = False
    Delay 3000
    if Status of UCX-11 Maglock BL = ReadyToLock {
        Color of \LIPA = 0:255:0
        Lock \MALO
        Write to \LCIN = "Mags Locked"
        Delay 3000
        OnOff_Off \EVC1
        OnOff_On \EVC2
        Delay 3000
        @connect
    }
}

@connect{
    * Uses Event Controller to trigger auto lock of Connector Main
    Write to \LCIN = "CHECKING CONNECTORS"
    Color of \LIPA = 255:255:0
    Delay 3000
    If Status of UCX-11 Connector Main = Connectable {
        Write to \LCIN = "CONNECTED MAIN"
        Lock \COMA
        Color of \LIPA = 0:255:0
        Delay 500
        Recharge \BATT
        Stockpile of \HYTA = True
    }
    Else If Status of UCX-11 Connector Second = Connectable {
        Write to \LCIN = "CONNECTED SECOND"
        Lock \COSE
        Color of \LIPA = 0:255:0
        Delay 500
        Recharge \BATT
        Stockpile of \HYTA = True
    }
    Else {
        WriteLine to \LCIN = "NO CONNECTORS"
        Color of \LIPA = 255:0:0
        Delay 3000
        Color of \LIPA = 255:255:255
    }
    Delay 3000
    @onlinetostandby
}

@launch{
    Write to \LCIN = "PREPPING FLIGHT"
    @offlinetostandby
    Discharge \BATT
    Stockpile of \HYTA = False
    Delay 3000
    Write to \LCIN = "Unlocking Connections"
    Unlock \COMA
    Unlock \COSE
    Unlock \MALO
    Delay 3000
    Write to \LCIN = "Flight Sys ON"
    Damp of MyShip = True
    @standbytoonline
    Delay 3000
    Write to \LCIN = "FLIGHT GO GO"
    OnOff_On \EVC1
}

@runmode{
    if Color of UCX-11 Light Status = 255:0:0
    {
        @offlinetostandby
    }
    else if Color of UCX-11 Light Status = 255:255:0
    {
        @standbytoonline
    }
    else if Color of UCX-11 Light Status = 255:255:255
    {
        @onlinetostandby
    }
    else if Color of UCX-11 Light Status = 255:255:50
    {
        @standbytooffline
    }
}

@offlinetostandby{
    Stop \TISH
    Stop \TIST
    OnOff_On \AIVE
    OnOff_On \HEVE
    OnOff_On \O2H2
    OnOff_On \ANTE
    OnOff_On \TURR
    OnOff_On \LIFL
    Color of \LIST = 255:255:0
    Color of \LIPA = 255:255:0
    Write to \LCIN = "Standby"
}

@standbytoonline{
    Damp of MyShip = True
    OnOff_On \GYRO
    OnOff_On \HYTA
    OnOff_On \THRU
    OnOff_On \LINA
    OnOff_On \LIPA
    Color of \LIST = 255:255:255
    Color of \LIPA = 255:255:255
    Write to \LCIN = "Active"
}

@onlinetostandby{
    Damp of MyShip = False
    OnOff_Off \GYRO
    OnOff_Off \HYTA
    OnOff_Off \THRU
    OnOff_Off \LINA
    OnOff_Off \LISE
    Color of \LIST = 255:255:50
    Color of \LIPA = 255:255:50
    Write to \LCIN = "Standby"
}

@standbytooffline{
    OnOff_Off \AIVE
    OnOff_Off \HEVE
    OnOff_Off \O2H2
    OnOff_Off \ANTE
    OnOff_Off \LIGH
    OnOff_Off \TURR
    Color of \LIST = 255:0:0
    Color of \LIPA = 255:0:0
    Write to \LCIN = "Powered Down"
}