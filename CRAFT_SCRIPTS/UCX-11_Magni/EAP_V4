@variables{
    * Abreviated names for blocks
    AIVE = "UCX-11 Air Vent Fan"
    ANTE = "UCX-11 Antenna"
    BATT = "UCX-11 Battery"
    COMA = "UCX-11 Connector Main"
    COSE = "UCX-11 Connector Second"
    EVC1 = "UCX-11 Event Controller - Land"
    EVC2 = "UCX-11 Event Controller - Cockpit"
    EVC3 = "UCX-11 Event Controller - Altitude"
    EVC4 = "UCX-11 Event Controller - Lock"
    GYRO = "UCX-11 Gyroscope"
    HEVE = "UCX-11 Heat Vent"
    HYTA = "UCX-11 Hydrogen Tank"
    LAMP = "UCX-11 Landing Magnetic Plate"
    LAMF = "UCX-11 Landing Magnetic Plate Front 1"
    LAMB = "UCX-11 Landing Magnetic Plate Back"
    LAND = "UCX-11 Landing Piston"
    LAPI = "UCX-11 Landing Piston"
    LCIN = "UCX-11 LCD Info"
    LIPA = "UCX-11 Light Panel Floor"
    LIST = "UCX-11 Light Status"
    LISE = "UCX-11 Light Search"
    LINA = "UCX-11 Light Nav"
    LIFL = "UCX-11 Light Flight"
    LIGH = "UCX-11 Light"
    MOCO = "UCX-11 Module Connector"
    MOCF = "UCX-11 Module Connector Front"
    MOHI = "UCX-11 Module Hinge"
    MOME = "UCX-11 Module Merge"
    MOMF = "UCX-11 Module Merge Front"
    O2H2 = "UCX-11 O2/H2 Generator"
    PRSC = "UCX-11 Program [SCC]"
    TURR = "UCX-11 Turret" 
    THRU = "UCX-11 Hydrogen Thruster"
    TISH = "UCX-11 Timer Shutdown"
    TIST = "UCX-11 Timer Standby"
}

@info {
    Write to \LCIN = EAP - V.3.66
    WriteLine to \LCIN = FSD - V0.39
    WriteLine to \LCIN = SDS - V0.00
    WriteLine to \LCIN = Craft - V1.35
}

@high_alt{
    * Triggered by Event Controller checking Altitude
    OnOff_Off \LIPA
    OnOff_On \PRSC
    Write to \LCIN = "HIGH ALTITUDE"
}

@low_alt{
    * Triggered by Event Controller checking Altitude
    Color of \LIPA = 255:255:255
    OnOff_On \LIPA
    OnOff_Off \PRSC
    Write to \LCIN = "LOW ALTITUDE"
}

@runmode{
    * Main functions of craft run through here
    if Color of UCX-11 Light Status = 255:0:0
    {
        @offlinetostandby
    }
    else if Color of UCX-11 Light Status = 255:255:0
    {
        @standbytoonline
    }
    else if Color of UCX-11 Light Status = 255:255:255
    {
        @onlinetostandby
    }
    else if Color of UCX-11 Light Status = 255:255:50
    {
        @standbytooffline
    }
}

@offlinetostandby{
    Stop \TIST
    Stop \TISH
    OnOff_On \ANTE
    OnOff_On \LIPA
    OnOff_On \LIST
    Delay 3000
    OnOff_On \EVC3
    OnOff_On \AIVE
    OnOff_On \TURR
    Delay 3000
    Color of \LIST = 255:255:0
    Color of \LIPA = 255:255:0
    Write to \LCIN = "Standby"
}

@standbytoonline{
    Stop \TIST
    Stop \TISH
    OnOff_On \LIFL
    OnOff_On \LINA
    OnOff_Off \EVC2
    Delay 3000
    Discharge \BATT
    Stockpile of \HYTA = False
    Delay 3000
    OnOff_On \GYRO
    Write to \LCIN = "Flight Sys ON"
    Damp of MyShip = True
    OnOff_On \THRU
    Delay 3000
    Unlock \LAMP
    Unlock \COMA
    Unlock \COSE
    Move \LAND to 0 at 0.5
    Color of \LIST = 255:255:255
    Color of \LIPA = 255:255:255
    Write to \LCIN = "Active"
}

@onlinetostandby{
    Write to \LCIN = "CHECKING STATUS"
    Delay 3000
    If Status of UCX-11 Landing Magnetic Plate Front 1 = ReadyToLock {
        Move \LAND to 2 at 0.2
        Delay 10000
        Lock \LAMP
        OnOff_Off \GYRO
        Damp of MyShip = False
        OnOff_Off \THRU
        Delay 3000
        OnOff_On \EVC2
        OnOff_Off \EVC3
        Delay 3000
        Color of \LIST = 255:255:50
        Color of \LIPA = 255:255:50
        Write to \LCIN = "Standby"
        Start \TISH
    }
    Else {
        WriteLine to \LCIN = "ERROR... Check Maglocks"
        Delay 3000
        WriteLine to \LCIN = "CANNOT LAND"
    }
}

@standbytooffline{
    Write to \LCIN = "CHECKING CONNECTORS"
    Delay 3000
    If Status of UCX-11 Connector Main = Connectable {
        WriteLine to \LCIN = "CONNECTED MAIN"
        Lock \COMA
        Delay 3000
        @recharge
    }
    Else If Status of UCX-11 Connector Second = Connectable {
        WriteLine to \LCIN = "CONNECTED SECOND"
        Lock \COSE
        Delay 3000
        @recharge
    }
    Else {
        WriteLine to \LCIN = "NO CONNECTORS"
        Delay 3000
        WriteLine to \LCIN = "CANNOT SHUT DOWN"
    }
}

@recharge{
    * Run by @standbytooffline for shutdown if connected
    OnOff_Off \AIVE
    Delay 3000
    OnOff_Off \ANTE
    OnOff_Off \TURR
    Delay 3000
    Recharge \BATT
    Stockpile of \HYTA = True
    Delay 3000
    Color of \LIST = 255:0:0
    Color of \LIPA = 255:0:0
    Write to \LCIN = "Powered Down"
    Delay 3000
    OnOff_Off \LIGH
}