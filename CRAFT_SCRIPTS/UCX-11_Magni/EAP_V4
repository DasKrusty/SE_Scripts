@variables{
    * Abreviated names for blocks
    AIVE = "UCX-11 Air Vent Fan"
    ANTE = "UCX-11 Antenna"
    BATT = "UCX-11 Battery"
    BEAC = "UCX-11 Beacon"
    COMA = "UCX-11 Connector Main"
    COSE = "UCX-11 Connector Second"
    EVC1 = "UCX-11 Event Controller - Emergency"
    EVC2 = "UCX-11 Event Controller - Cockpit"
    EVC3 = "UCX-11 Event Controller - Altitude"
    EVC4 = "UCX-11 Event Controller - Lock"
    GYRO = "UCX-11 Gyroscope"
    HEVE = "UCX-11 Heat Vent"
    HYTA = "UCX-11 Hydrogen Tank"
    LAMP = "UCX-11 Landing Magnetic Plate"
    LAMF = "UCX-11 Landing Magnetic Plate Front 1"
    LAMB = "UCX-11 Landing Magnetic Plate Back"
    LAND = "UCX-11 Landing Piston"
    LAPI = "UCX-11 Landing Piston"
    LCIN = "UCX-11 LCD Info"
    LIPA = "UCX-11 Light Panel Floor"
    LIST = "UCX-11 Light Status"
    LISE = "UCX-11 Light Search"
    LINA = "UCX-11 Light Nav"
    LIFL = "UCX-11 Light Flight"
    LIGH = "UCX-11 Light"
    MOCO = "UCX-11 Module Connector"
    MOCF = "UCX-11 Module Connector Front"
    MOHI = "UCX-11 Module Hinge"
    MOME = "UCX-11 Module Merge"
    MOMF = "UCX-11 Module Merge Front"
    O2H2 = "UCX-11 O2/H2 Generator"
    PAHA = "UCX-11 Parachute Hatch"
    PRSC = "UCX-11 Program [SCC]"
    TURR = "UCX-11 Turret" 
    THRU = "UCX-11 Hydrogen Thruster"
    TISH = "UCX-11 Timer Shutdown"
    TIST = "UCX-11 Timer Standby"
}

@info {
    Write to \LCIN = EAP - V.4.88
    WriteLine to \LCIN = FSD - V0.39
    WriteLine to \LCIN = SDS - V0.00
    WriteLine to \LCIN = Craft - V1.73
}

@high_alt{
    * Triggered by Event Controller checking Altitude
    OnOff_Off \LIPA
    Write to \LCIN = "HIGH ALTITUDE"
}

@low_alt{
    * Triggered by Event Controller checking Altitude
    Color of \LIPA = 255:255:255
    OnOff_On \LIPA
    Write to \LCIN = "LOW ALTITUDE"
}

@runmode{
    * Main functions of craft run through here
    if Color of UCX-11 Light Status = 255:0:0
    {
        @offlinetostandby
    }
    else if Color of UCX-11 Light Status = 255:255:0
    {
        @standbytoonline
    }
    else if Color of UCX-11 Light Status = 255:255:255
    {
        @onlinetostandby
    }
    else if Color of UCX-11 Light Status = 255:255:50
    {
        @standbytooffline
    }
}

@offlinetostandby{
    OnOff_On \LIPA
    OnOff_On \LIST
    Color of \LIST = 255:255:0
    Color of \LIPA = 255:255:0
    Write to \LCIN = "IN STANDBY"
    Stop \TIST
    Stop \TISH
    OnOff_On \ANTE
    Delay 1000
    OnOff_On \EVC3
    OnOff_On \AIVE
    OnOff_On \TURR
    Delay 1000
    Color of \LIPA = 0:255:0
    Delay 1000
    Color of \LIPA = 255:255:0
}

@standbytoonline{
    Color of \LIST = 255:255:255
    Color of \LIPA = 255:255:255
    Write to \LCIN = "PREPPING FLIGHT"
    OnOff_Off \BEAC
    Stop \TIST
    Stop \TISH
    OnOff_On \LIFL
    OnOff_On \LINA
    Delay 1000
    Discharge \BATT
    Stockpile of \HYTA = False
    Delay 1000
    OnOff_On \GYRO
    WriteLine to \LCIN = "FLIGHT READY"
    Damp of MyShip = True
    OnOff_On \THRU
    Color of \LIPA = 0:0:255
    Delay 1000
    Color of \LIPA = 255:255:255
    Delay 1000
    Unlock \LAMP
    Unlock \COMA
    Unlock \COSE
    WriteLine to \LCIN = "ACTIVE"
    Move \LAND to 0 at 0.5
    Color of \LIPA = 0:255:0
    Delay 1000
    Color of \LIPA = 255:255:255
}

@onlinetostandby{
    Write to \LCIN = "CHECKING STATUS"
    Color of \LIST = 255:255:50
    Color of \LIPA = 255:255:50
    Delay 1000
    If Status of UCX-11 Connector Main = Connectable {
        WriteLine to \LCIN = "CONNECTED UNDER"
        Lock \COMA
        Color of \LIST = 0:0:250
        Delay 3000
        @flightoff
    }
    Else If Status of UCX-11 Connector Second = Connectable {
        WriteLine to \LCIN = "CONNECTED BACK"
        Lock \COSE
        Color of \LIST = 0:0:250
        Delay 3000
        @flightoff
    }
    Else If Status of UCX-11 Landing Magnetic Plate Front 1 = ReadyToLock {
        WriteLine to \LCIN = "READY TO LOCK"
        Delay 1000
        WriteLine to \LCIN = "WAIT ONE"
        Move \LAND to 2 at 0.3
        Damp of MyShip = False
        Delay 9000
        Lock \LAMP
        OnOff_Off \EVC3
        @flightoff
    }
    If Gravity of MyShip < 0.05 {
        WriteLine to \LCIN = "NO GRAVITY"
        Delay 1000
        @flightoff
    }
    Else {
        WriteLine to \LCIN = "NOT READY TO LOCK"
        Delay 1000
        Color of \LIST = 255:255:255
        Color of \LIPA = 255:255:255
    }
}

@standbytooffline{
    OnOff_Off \AIVE
    OnOff_Off \ANTE
    OnOff_Off \TURR
    Delay 3000
    if Color of UCX-11 Light Status = 0:0:250 {
        @recharge
    }
    Else {
        WriteLine to \LCIN = "NO CONNECTION"
        Delay 3000
        WriteLine to \LCIN = "CANNOT SHUT DOWN"
        @offlinetostandby
    }
    Color of \LIST = 255:0:0
    Color of \LIPA = 255:0:0
    Write to \LCIN = "POWERED DOWN"
    OnOff_Off \LIGH
}

@recharge{
    * Run by @standbytooffline for shutdown if connected
    Recharge \BATT
    Stockpile of \HYTA = True
}

@emergency{
    OnOff_Off \THRU
    OnOff_Off \GYRO
    OnOff_On \BEAC
    Open_On \PAHA
    Write to \LCIN = "ASSISTANCE REQUIRED"
}

@flightoff{
    * Used by @onlinetostandby
    OnOff_Off \GYRO
    OnOff_Off \THRU
    Damp of MyShip = False
    OnOff_On \LIPA
    Color of \LIPA = 0:255:0
    Delay 1000
    Color of \LIPA = 255:255:50
    OnOff_Off \LIPA
    Start \TISH
    Write to \LCIN = "STANDBY"
}